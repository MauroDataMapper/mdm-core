buildscript {
    repositories {
        mavenLocal()
        maven {url "https://jenkins.cs.ox.ac.uk/artifactory/plugins-snapshot"}
        maven {url "https://jenkins.cs.ox.ac.uk/artifactory/plugins-release"}
        maven {url "https://repo.grails.org/grails/core"}
    }
    dependencies {
        classpath "uk.ac.ox.softeng.maurodatamapper.gradle:mdm-gradle-plugin:$mdmGradlePluginVersion"
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.grails.plugins:hibernate5:$grailsHibernate5Version"
        classpath "org.grails.plugins:views-gradle:$grailsViewsVersion"
    }
}

apply plugin: "idea"
apply plugin: "war"
apply plugin: "org.grails.grails-web"
apply plugin: "org.grails.plugins.views-json"
apply plugin: "org.grails.plugins.views-markup"

apply plugin: 'uk.ac.ox.softeng.maurodatamapper.gradle.mdm-grails'

repositories {
    mavenLocal()
    maven {url "https://jenkins.cs.ox.ac.uk/artifactory/libs-release"}
    maven {url "https://jenkins.cs.ox.ac.uk/artifactory/libs-snapshot"}
    maven {url "https://repo.grails.org/grails/core"}
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-codecs"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-datasource"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:events"
    implementation "org.grails.plugins:hibernate5"
    implementation "org.hibernate:hibernate-core"
    implementation "org.grails.plugins:views-json"
    implementation "org.grails.plugins:views-json-templates"
    implementation "org.grails.plugins:views-markup"
    implementation "com.bertramlabs.plugins:asset-pipeline-grails:$assetPipelineVersion"
    profile "org.grails.profiles:rest-api"
    runtimeOnly "org.glassfish.web:el-impl:2.2.1-b05"
    integrationTestRuntimeOnly "com.h2database:h2"
    runtimeOnly "org.apache.tomcat:tomcat-jdbc"
    runtimeOnly "javax.xml.bind:jaxb-api:$jaxbApiVersion"
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails:views-json-testing-support"
    testImplementation "org.hibernate.validator:hibernate-validator"
    testImplementation "io.micronaut:micronaut-http-client"
}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always',
        '-noverify',
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main

    System.properties.each {prop ->
        systemProperty prop.key, prop.value
    }

    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
    systemProperty 'user.dir', projectDir
}

grails {
    plugins {
        implementation project(':mdm-core')
        implementation project(':mdm-plugin-authentication-basic')
        implementation project(':mdm-plugin-authentication-apikey')
        implementation project(':mdm-plugin-datamodel')
        implementation project(':mdm-plugin-terminology')
        implementation project(':mdm-plugin-referencedata')
        implementation project(':mdm-plugin-federation')
        //        compile project(':mdm-plugin-email-proxy')
        implementation project(':mdm-security')
        implementation project(':mdm-plugin-dataflow')
        implementation project(':mdm-plugin-profile')
        implementation project(':mdm-plugin-freemarker')
        implementation project(':mdm-plugin-xsd')
    }
}

apply from: 'dependencies.gradle'

String testCategory = ''
String testPackage = ''

if (System.getProperty('gradle.test.category')) {
    testCategory = System.getProperty('gradle.test.category')
}
if (System.getProperty('gradle.test.package')) {
    testPackage = System.getProperty('gradle.test.package')
}

integrationTest {
    //    failFast true
    useJUnitPlatform()

    if (testPackage) {
        logger.quiet('Filtering to tests on {}', "uk.ac.ox.softeng.maurodatamapper.testing.functional.${testPackage}.*")
        filter {
            includeTestsMatching "uk.ac.ox.softeng.maurodatamapper.testing.functional.${testPackage}.*"
        }

        binaryResultsDirectory.set(file("${project.testResultsDir}/${testPackage}/binary"))
        reports {
            junitXml.getOutputLocation().set file("${project.testResultsDir}/${testPackage}")

        }
        mergeTestReports.reportOn(file("${project.testResultsDir}/${testPackage}/binary"))

        systemProperty('mdm.logFileName', "e2e-${testPackage}")
    }

    if (testCategory) {
        binaryResultsDirectory.set(file("${project.testResultsDir}/${testCategory}/binary"))
        reports {
            junitXml.getOutputLocation().set file("${project.testResultsDir}/${testCategory}")

        }
        mergeTestReports.reportOn(file("${project.testResultsDir}/${testCategory}/binary"))
    }
}

afterEvaluate {

    tasks.withType(AbstractPublishToMaven).configureEach {
        enabled = false
    }
    sonarqube {
        properties {
            property 'sonar.sources', ''
            property 'sonar.java.binaries', ''
            property 'sonar.groovy.binaries', ''
            property 'sonar.tests', findAllExisting(project, project.sourceSets.integrationTest.allSource.srcDirs +
                                                             project.sourceSets.main.allSource.srcDirs)
            property 'sonar.java.test.binaries', project.sourceSets.integrationTest.output.classesDirs +
                                                 project.sourceSets.main.output.classesDirs
        }
    }
}

List<File> findAllExisting(Project project, Collection<File> files) {
    files.findAll {it.exists()}.collect {project.projectDir.relativePath(it)}
}